plugins {
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'nu.studer.jooq' version '5.1.1'
}

group = 'com.cockroachdb'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

processResources {
    expand(project.properties)
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.postgresql:postgresql'

    implementation 'org.jooq:jooq'
    implementation 'org.jooq:jooq-codegen'
    jooqGenerator 'org.postgresql:postgresql'

    compileOnly 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

jooq {
    version = dependencyManagement.importedProperties['jooq.version']
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = (String) project.property("url")
                    user = (String) project.property("username")
                    password = (String) project.property("password")
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'

                    target {
                        packageName = 'db'
                        directory = 'src/main/java/com/cockroachdb/hacktoberfest/model/jooq'
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}